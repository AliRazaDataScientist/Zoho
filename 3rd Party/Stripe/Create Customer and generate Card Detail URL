void create_Stripe_Customer(map payload)
{
dataMap = Map();
email = payload.get("email");
name = payload.get("name");
dataMap.put("email",payload.get("email"));
dataMap.put("name",payload.get("name"));
dataMap.put("phone",payload.get("phone"));
dataMap.put("address[line1]",payload.get("address__line1__"));
dataMap.put("address[city]",payload.get("address__city__"));
dataMap.put("address[state]",payload.get("address__state__"));
dataMap.put("address[postal_code]",payload.get("address__postal_code__"));
dataMap.put("address[country]",payload.get("address__country__"));
// Call Stripe API to create customer
Customer = invokeurl
[
	url :"https://api.stripe.com/v1/customers"
	type :POST
	parameters:dataMap
	connection:"stripe_connection_live"
];
customerId = Customer.get("id");
info "Customer Created: " + customerId;
creatorUpdate_Record = Map();
creatorUpdate_Record.put("Stripe_Customer_Id",customerId);
res = zoho.creator.updateRecord("ncasel_staycqusa","staycqusa","Client_Report",payload.get("id").toLong(),creatorUpdate_Record,Map(),"flow_to_creator");
info res;
///////////////////////////////////////////////////
setupResp = invokeurl
[
	url :"https://api.stripe.com/v1/setup_intents"
	type :POST
	parameters:{"customer":customerId,"payment_method_types[]":"card"}
	connection:"stripe_connection_live"
];
client_secret = setupResp.get("client_secret");
setup_intent_id = setupResp.get("id");
checkoutParams = Map();
checkoutParams.put("mode","setup");
checkoutParams.put("customer",customerId);
checkoutParams.put("currency","USD");
checkoutParams.put("payment_method_types[0]","card");
checkoutParams.put("success_url","https://stripe.com");
checkoutParams.put("cancel_url","https://stripe.com");
checkoutParams.put("setup_intent_data[description]","Save card for future payments");
// Call Stripe API
checkoutResp = invokeurl
[
	url :"https://api.stripe.com/v1/checkout/sessions"
	type :POST
	parameters:checkoutParams
	connection:"stripe_connection_live"
];
checkoutUrl = checkoutResp.get("url");
info checkoutUrl;
if(email != "" && email != null)
{
	msg = "Dear " + name + ",<br><br>You can complete your payment by visiting the following checkout link: <a href=" + checkoutUrl + ">Checkout URL</a>.<br>Please enter your card details on the page to finalize the payment.<br><br>Thank you for your prompt action.<br><br>Best regards,<br>STAY CQUSA";
	sendmail
	[
		from :zoho.loginuserid
		to :email
		subject :"Enter Your Card Details"
		message :msg
	]
}
}
