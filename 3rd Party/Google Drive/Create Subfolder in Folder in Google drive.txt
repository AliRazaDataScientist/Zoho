///// Generate access token here
paramMap = Map();
paramMap.put("client_id",client_id);
paramMap.put("client_secret",client_secret);
paramMap.put("refresh_token",refresh_token);
paramMap.put("grant_type","refresh_token");
response = invokeurl
[
	url :"https://oauth2.googleapis.com/token"
	type :POST
	parameters:paramMap.toMap()
	content-type:"application/x-www-form-urlencoded"
];
accessToken = response.get("access_token");
headersMap = Map();
headersMap.put("Authorization","Bearer " + accessToken);
headersMap.put("Content-Type","application/json");
////////////
rec = zoho.crm.getRecordById("Deals",id);
accountid = rec.getJSON("Account_Name").getJSON("id");
rec = zoho.crm.getRecordById("Accounts",accountid);
Google_Drive_Folder_Id = rec.getJSON("Google_Drive_Folder_Id");
SubFolder_Map = Map();
if(Google_Drive_Folder_Id == null)
{
	folderName = rec.getJSON("Account_Name");
	/////Creating folder here
	parentFolderId = "0AHkRhT7wIfT3Uk9PVA";
	parentList = list();
	parentList.add(parentFolderId);
	requestBodyMap = Map();
	requestBodyMap.put("name",folderName);
	requestBodyMap.put("mimeType","application/vnd.google-apps.folder");
	requestBodyMap.put("parents",parentList);
	response = invokeurl
	[
		url :"https://www.googleapis.com/drive/v3/files?supportsAllDrives=true"
		type :POST
		body:requestBodyMap.toString()
		headers:headersMap
	];
	Google_Drive_Folder_Id = response.getJSON("id");
	update_map = Map();
	update_map.put("Google_Drive_Folder_Id",Google_Drive_Folder_Id);
	update_rec = zoho.crm.updateRecord("Accounts",accountid,update_map);
	info update_rec;
}
subfolders = {"AV","Decor","Entertainment","F&B","Final Billing","Graphics","Hotel","Housing","Internal B Line","Photographer/Videographer","Registration","SOW","Transportation","Travel Directors","Vendors"};
for each  foldername in subfolders
{
	/////Creating folder here
	parentList = list();
	parentList.add(Google_Drive_Folder_Id);
	requestBodyMap = Map();
	requestBodyMap.put("name",foldername);
	requestBodyMap.put("mimeType","application/vnd.google-apps.folder");
	requestBodyMap.put("parents",parentList);
	response = invokeurl
	[
		url :"https://www.googleapis.com/drive/v3/files?supportsAllDrives=true"
		type :POST
		body:requestBodyMap.toString()
		headers:headersMap
	];
	Google_Drive_SubFolder_Id = response.getJSON("id");
	SubFolder_Map.put(foldername,Google_Drive_SubFolder_Id);
}
info SubFolder_Map;
deal_rec = zoho.crm.updateRecord("Deals",id,{"Sub_Folders_ID":SubFolder_Map,"Google_Drive_Link":"https://drive.google.com/drive/folders/" + Google_Drive_Folder_Id});
info deal_rec;