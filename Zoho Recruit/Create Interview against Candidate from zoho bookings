void Create_Interview_In_Zoho_Recruit(string interviewer_email, string email, string end_date, string start_date)
{
dt1 = start_date.toDateTime("dd-MMM-yyyy HH:mm:ss");
start_datetime = dt1.toString("yyyy-MM-dd'T'HH:mm:ssXXX");
start_datetime = start_datetime.replaceAll("([+-][0-9]{2}:?[0-9]{2}|Z)$","-05:00");
dt2 = end_date.toDateTime("dd-MMM-yyyy HH:mm:ss");
end_datetime = dt2.toString("yyyy-MM-dd'T'HH:mm:ssXXX");
end_datetime = end_datetime.replaceAll("([+-][0-9]{2}:?[0-9]{2}|Z)$","-05:00");
interviewer_details = invokeurl
[
	url :"https://recruit.zoho.com/recruit/v2/users?type=AllUsers"
	type :GET
	connection:"zohorecruit"
];
rec_data = interviewer_details.getJSON("users");
for each  user in rec_data
{
	user_email = user.getJSON("email");
	if(user_email == interviewer_email)
	{
		interviewer_id = user.getJSON("id");
	}
}
candidate_details = zoho.recruit.searchRecords("Candidates","Email|=|" + email,1,3,"Last Name,Email","zohorecruit");
candidate_id = candidate_details.getJSON("CANDIDATEID");
associated_applications = invokeurl
[
	url :"https://recruit.zoho.com/recruit/v2/Candidates/" + candidate_id + "/Applications"
	type :GET
	connection:"zohorecruit"
];
app_data = associated_applications.getJSON("data");
job_id = app_data.getJSON("$Job_Opening_Id");
client_id = app_data.getJSON("Client_Name").getJSON("id");
data_list = List();
data_map = Map();
// Client_Name (Map)
client_map = Map();
client_map.put("id",client_id);
data_map.put("Client_Name",client_map);
// Candidate_Name (Map)
candidate_map = Map();
candidate_map.put("id",candidate_id);
data_map.put("Candidate_Name",candidate_map);
// Simple String Fields
data_map.put("Interviewer",interviewer_id);
data_map.put("Interview_Name","Online Interview");
data_map.put("Start_DateTime",start_datetime);
data_map.put("Reminder","1 day before");
data_map.put("End_DateTime",end_datetime);
// Job Opening Name (Map)
jobopening_map = Map();
jobopening_map.put("id",job_id);
data_map.put("Job_Opening_Name",jobopening_map);
// Add map into list
data_list.add(data_map);
// Final deluge structure
final_map = Map();
final_map.put("data",data_list);
info final_map;
rec_info = invokeurl
[
	url :"https://recruit.zoho.com/recruit/v2/Interviews"
	type :POST
	parameters:final_map.toString()
	connection:"zohorecruit"
];
info rec_info;
}
